````markdown
# Einrichtung der Feature-Auswahl vom Map-Canvas

Um die Feature-Auswahl vom Map-Canvas einzurichten, m√ºssen wir einen QField pointHandler registrieren, √§hnlich wie ein QgsMapTool in pyQGIS. Dies geschieht in der Hauptcode-Datei demo2_selection.qml.  

Dabei lernen wir auch:

- wie man einen Layer aus einem Projekt erh√§lt
- wie man ein Feature mit einem Ausdruck erh√§lt
- wie man ein Attribut aus einem Feature erh√§lt

## Was machen wir?

- einen Point-Handler abrufen
- die geklickten Kartenkoordinaten vom Point-Handler abrufen
- ein 20m-Begrenzungsrechteck um die Kartenkoordinaten zeichnen
- eine r√§umliche Abfrage verwenden, um Features auf dem Plot-Layer zu finden, die sich innerhalb des Begrenzungsrechtecks befinden
- wenn ein Feature gefunden wird, das Plugin aktivieren und die Plot-ID des Features an die Plugin-Component √ºbergeben

## 1. Den Point-Handler von der Schnittstelle abrufen

Abrufen und speichern Sie einen Verweis auf den pointHandler der QField-Schnittstelle als Klassen-Property.

```qml
Item{
    id: plugin
    property var pointHandler: iface.findItemByObjectName("pointHandler")

}
```

- iface ist Ihre QField-Schnittstelleninstanz, wie qgis.utils.iface in pyqgis. Sie wird aus org.qfield importiert.
- Wir haben bereits die Verwendung von iface.logMessage und iface.addItemToPluginsToolbar gesehen
- iface.findItemByObjectName ist eine QField-Schnittstellenfunktion, die jedes Objekt aus der QField-Instanz abruft, sofern es eine objectName-Property hat. (Hier beginnen Sie, durch den Quellcode zu graben.)
- pointHandler ist ein QField-Schnittstellenobjekt, das die Koordinaten einer Karteninteraktion und die Art der Interaktion (Klick, Doppelklick, Halten und Dr√ºcken) ausgibt

## 2. Wenn das Plugin l√§dt (wenn Ihr Projekt √∂ffnet), registrieren Sie Ihre benutzerdefinierte Callback-Funktion f√ºr den QField Point-Handler

Die onCompleted-Funktion des Root-Items wird ausgel√∂st, wenn das Plugin l√§dt.

```qml
Item{
    id: plugin
    property var pointHandler: iface.findItemByObjectName("pointHandler")
    Component.onCompleted{
        pointHandler.registerHandler("demo2_selection", my_callback);
    }
}
```

## 3. Wenn das Plugin entladen wird (wenn Ihr Projekt schlie√üt), deregistrieren Sie Ihre Callback-Funktion!

Wenn Sie das nicht tun, wird Ihr Point-Handler Ihre anderen Projekte kontaminieren. 

Verwenden Sie Component.onDestruction, um Verhalten beim Schlie√üen zu definieren.

```qml
Item{
    id: plugin
    <...>
    Component.onDestruction{
        pointHandler.deregisterHandler("demo2_selection");
    }
}
```

## 4. Definieren Sie Ihren Callback mit einer Arrow-Funktion

Unsere Callback-Funktion ist in JavaScript geschrieben. Anstatt eine Funktionsreferenz wie unten zu verwenden, wie es ein Python-Programmierer tun w√ºrde:

```qml
Item{
    id: plugin
    <...>
    Component.onCompleted{
        pointHandler.registerHandler("demo2_selection", my_callback);
    }
}
```

ist es √ºblicher, eine JavaScript-Arrow-Funktions-Syntax zu sehen, wie wir sie in demo2_selection sehen werden

```qml
Item{

    Component.onCompleted{
        pointHandler.registerHandler("demo2_selection", (point, type, interactionType)=>{
            iface.logMessage("Interaction Type: " + interactionType)
            return true
        });
    }
}

```
### Vergessen Sie nicht den booleschen R√ºckgabewert Ihres Callbacks
Der boolesche R√ºckgabewert vom pointHandler-Callback teilt QField mit, ob Ihr Handler das Event verbraucht hat oder nicht:

return true - Event verbraucht

Ihr Handler hat den Klick verarbeitet
- QField sollte aufh√∂ren, das Event an andere Handler weiterzugeben
- Verhindert QFields Standardverhalten f√ºr diesen Klick

return false oder kein return - Event nicht verbraucht

- Ihr Handler hat den Klick nicht verarbeitet (oder m√∂chte ihn durchgeben)
- QField gibt weiter an andere registrierte Handler
- QFields Standardverhalten kann immer noch ausgef√ºhrt werden

## 5. Definieren Sie den Callback, um Kartenkoordinaten von der Benutzerinteraktion zu erhalten

### Entscheiden Sie, welche Interaktion Sie wollen

- "clicked" :   
  Das Ausl√∂sen bei Einzelklick kann Konflikte verursachen, da die Feature-Drawer bei einem Einzelklick √∂ffnet.  
  - In QField f√ºr Windows wird der Einzelklick die Feature-Drawer blockieren und durch unser Plugin ersetzen.   
  - In QField f√ºr iOS wird, wenn Sie einen Einzelklick verwenden, die Attributtabelle √ºber dem Plugin gerendert.

- "doubleClicked" :  
  Das Ausl√∂sen bei Doppelklick verhindert Konflikte. Wir k√∂nnen die Feature-Drawer bei einem Einzelklick √∂ffnen lassen und mit einem Doppelklick in das Plugin eintreten.  
  - In QField f√ºr Windows (im Moment) registriert die Doppelklick-Interaktion nicht. 
  - In iOS funktioniert das gro√üartig.

- "pressAndHold":  
   Dies w√§re theoretisch auch ein sch√∂ner Modus zum √ñffnen unseres Plugins, aber in der Praxis ist es eine schlechte Wahl
  - In iOS blockiert das Kontextmen√º, das sich beim Dr√ºcken √∂ffnet, das √ñffnen unseres Plugins bei dieser Interaktion.
  - In Windows registriert die Press-and-Hold-Interaktion nicht.

Bedeutet das, dass der boolesche R√ºckgabewert nicht ganz wie angek√ºndigt funktioniert? Vielleicht. Es gibt auch eine Priorit√§t, die auf pointHandler-Callbacks gesetzt wird, die die Verarbeitung des booleschen R√ºckgabewerts aus dem Callback beeinflussen kann.

Um Konflikte mit QField zu vermeiden und sicherzustellen, dass unser Plugin in beiden Umgebungen funktioniert, verwenden Sie clicked, wenn wir in Windows sind, und doubleClicked, wenn wir in iOS sind.

### Vergessen Sie Ihr Return nicht

- return true nach der Behandlung Ihres Punktes, um andere Handler wie die QField-Attributtabelle am Ausl√∂sen zu hindern.
- return false, wenn Sie die Interaktion ignorieren, damit der n√§chste Handler sie aufnehmen kann.

```qml

Item{
    Component.onCompleted{
        pointHandler.registerHandler("demo2_selection", (point, type, interactionType) => {
            var shouldHandle = (Qt.platform.os === "windows" && interactionType === "clicked") ||
                               (Qt.platform.os !== "windows" && interactionType === "doubleClicked")
            if (shouldHandle) {
                iface.logMessage("Platform " + Qt.platform.os)
                iface.logMessage("Interaction " + interactionType)      
                return true // weitere Verarbeitung des Klicks blockieren
            }
            return false // die Interaktion von QField aufnehmen lassen
        });
    }
}

```
### Die Pixelkoordinaten der Bildschirminteraktion abrufen

```qml

pointHandler.registerHandler("demo2_searchbar", (point, type, interactionType) => {
    var shouldHandle = ... 
    if (shouldHandle){
      iface.logMessage(point.x)
      iface.logMessage(point.y)
    }
});
```

### In Kartenkoordinaten umwandeln
```qml

pointHandler.registerHandler("demo2_searchbar", (point, type, interactionType) => {
    var shouldHandle = ... 
    if (shouldHandle){
      let coords = iface.mapCanvas().mapSettings.screenToCoordinate(Qt.point(point.x, point.y))
      iface.logMessage(coords.x)
      iface.logMessage(coords.y)
    }
});
```

## 6. Definieren Sie Ihren Callback, um eine r√§umliche Abfrage auf dem Plot-Layer mit Ihren Benutzerkoordinaten auszuf√ºhren

Wir werden eine r√§umliche Abfrage auf dem Plots-Layer mit einem Begrenzungsrechteck von 20m um unsere geklickten Koordinaten ausf√ºhren. Wenn wir ein Feature in diesem Rechteck finden, werden wir das Plugin aktivieren und die Plot-ID des Features an unsere Plugin-Component √ºbergeben.

### Den Plots-Layer aus dem Projekt abrufen

Sie k√∂nnen eine Teilmenge der QGIS-API-Funktionen von QField und JavaScript aufrufen. Sie k√∂nnen herausfinden, welche Funktionen von QField aufgerufen werden k√∂nnen, indem Sie zur QGIS C++ Class Reference f√ºr die Klasse gehen, an der Sie interessiert sind. Funktionen, die Sie aufrufen k√∂nnen, sind mit Q_INVOKABLE gekennzeichnet.

![alt text](img/demo2_reference.png)

Wir holen den Map-Layer aus der QgsProject-Instanz, die in QField als qgisProject verf√ºgbar ist, das mit org.qgis importiert wurde.

```qml

pointHandler.registerHandler("demo2_searchbar", (point, type, interactionType) => {
    var shouldHandle = ... 
    if (shouldHandle){
      let layer = qgisProject.mapLayersByName("plots")[0]
      iface.logMessage("Got plots layer")
    }
});
```

### Ein Feature aus dem Plots-Layer mit einem LayerUtils Feature-Iterator mit einem einfachen Ausdruck abrufen

getFeatures ist noch keine aufrufbare QGIS-Funktion. Stattdessen k√∂nnen wir einen Ausdruck √ºbergeben, um einen Feature-Iterator aus QFields LayerUtils-Klasse zu erhalten, die aus org.qfield importiert wird.  

Wir werden unseren Plots-Layer nach dem Feature mit plot_id = 'b.1' abfragen.

Wir werden dieses Feature abrufen und seine Plot-ID ausgeben. (Noch nicht unsere Koordinaten verwenden.)

```qml

pointHandler.registerHandler("demo2_searchbar", (point, type, interactionType) => {
    var shouldHandle = ... 
    if (shouldHandle){
      var layer = qgisProject.mapLayersByName("plots")[0]
      var expression = "plot_id = 'b.1'";

      let it = LayerUtils.createFeatureIteratorFromExpression(layer, expression)
      
      if (it.hasNext()){
        feature = it.next()
        plot_id = feature.attribute("plot_id")
        iface.logMessage("found the feature of the plots layer: " + plot_id)
      }
      it.close(); // NIEMALS vergessen, Ihren Iterator zu schlie√üen
      return true
    }
    return false

});
```
#### WICHTIG: Vergessen Sie niemals, einen Feature-Iterator zu schlie√üen. Wenn Sie Ihren Feature-Iterator nicht schlie√üen, f√ºhrt dies zu einem vollst√§ndigen Herunterfahren von QField, nach etwa dem 4. Mal, dass Sie den Feature-Iterator aufrufen.  


### Tauschen Sie nun den einfachen Ausdruck mit einer r√§umlichen Abfrage aus

Wir werden ein Begrenzungsrechteck von 20m um unsere Interaktionskoordinaten erstellen.  
Wir werden die Begrenzungsrechteck-Koordinaten verwenden, um eine Schnittmengen-Abfrage an LayerUtils zu √ºbergeben, anstelle unserer einfachen Abfrage.

```qml

      if (shouldHandle) {
        // Erstelle ein Paar von Punkten, die einen Pufferbereich darstellen, in dem Features gesucht werden sollen. 
        let tl = mapCanvas.mapSettings.screenToCoordinate(Qt.point(point.x - 20, point.y - 20))
        let br = mapCanvas.mapSettings.screenToCoordinate(Qt.point(point.x + 20, point.y + 20))

        let expression = "intersects(geom_from_wkt('POLYGON(("+tl.x+" "+tl.y+", "+br.x+" "+tl.y+", "+br.x+" "+br.y+", "+tl.x+" "+br.y+", "+tl.x+" "+tl.y+"))'), $geometry)"
        let it = LayerUtils.createFeatureIteratorFromExpression(qgisProject.mapLayersByName("plots")[0], expression)
        if (it.hasNext()) {
          const feature = it.next()
          console.log(feature.id + " " + feature.attribute("plot_id"))
        }
        it.close();
      }
      return false
    
```

### Schlie√ülich, wenn Sie das Feature finden, schalten Sie das Plugin ein und √ºbergeben Sie die Plot-ID

Die Loader-Klasse hat die Property "item", die das Item enth√§lt, das von seiner Source-Component geladen wurde.

In unserer Plugin-Component haben wir eine Funktion setPlotId definiert, die eine Plot-ID erh√§lt und sie verwendet, um die Suchleiste und Textbox des Plugins zu f√ºllen.

**Denken Sie daran, den Iterator zu schlie√üen, unabh√§ngig davon, ob das Feature gefunden wird oder nicht!**

**Denken Sie daran, den booleschen Wert f√ºr den pointHandler zur√ºckzugeben. **

```qml
    pointHandler.registerHandler("demo2_selection", (point, type, interactionType) => {
      // ...
      if (shouldHandle) {
        // ...
        if (it.hasNext()) {
          // Holen Sie Ihr Feature
          const feature = it.next()

          // Schlie√üen Sie Ihren Iterator
          it.close()  
          
          // Schalten Sie das Plugin ein, genau wie die Kamera-Schaltfl√§che
          pluginLoader.active = true
          
          // √úbergeben Sie die Plot-ID an die Plugin-Component
          pluginLoader.item.plotId = feature.attribute("plot_id")
          
          // Blockieren Sie das Interaktionssignal
          return true
        }
        // Schlie√üen Sie den Iterator, wenn Sie kein Feature finden
        it.close();
      }
      // Geben Sie die Interaktion weiter
      return false
    });
```

## Die gesamte demo2_selection.qml

```qml
 // imports

Item {
  id: plugin
  parent: iface.mapCanvas() 
  anchors.fill: parent 
  
  // Map Selection: 1. Halten Sie einen Verweis auf das Map-Canvas
  property var mapCanvas: iface.mapCanvas() 
  
  // Map Selection: 2. F√ºgen Sie den pointHandler zum Plugin hinzu
  property var pointHandler: iface.findItemByObjectName("pointHandler")

  Loader {
    id: pluginLoader
    // ...
  }  


  Component.onCompleted: {
 
    // Map Selection: 3. Registrieren Sie den Point-Handler und definieren Sie seinen Callback
    pointHandler.registerHandler("demo2_searchbar", (point, type, interactionType) => {

      // Entscheiden Sie √ºber die Interaktion  
      var shouldHandle = (Qt.platform.os === "windows" && interactionType === "clicked") ||
                         (Qt.platform.os !== "windows" && interactionType === "doubleClicked")
      if (shouldHandle) {

        // Erstellen Sie ein Paar von Punkten, die einen 20m-Pufferbereich darstellen, in dem Features gesucht werden sollen. 
        let tl = mapCanvas.mapSettings.screenToCoordinate(Qt.point(point.x - 20, point.y - 20))
        let br = mapCanvas.mapSettings.screenToCoordinate(Qt.point(point.x + 20, point.y + 20))

        // F√ºhren Sie eine r√§umliche Abfrage aus
        let expression = "intersects(geom_from_wkt('POLYGON(("+tl.x+" "+tl.y+", "+br.x+" "+tl.y+", "+br.x+" "+br.y+", "+tl.x+" "+br.y+", "+tl.x+" "+tl.y+"))'), $geometry)"
        let it = LayerUtils.createFeatureIteratorFromExpression(qgisProject.mapLayersByName("plots")[0], expression)
        if (it.hasNext()) {
          // Sie haben ein Feature, spielen Sie damit! :)
          const feature = it.next()
          console.log(feature.id)
          it.close()
          pluginLoader.active = true
          // √úbergeben Sie die Plot-ID an die Plugin-Component
          pluginLoader.item.plotId = feature.attribute("plot_id")
          return true
        }
        it.close();
      }
      return false
    });
  }

  // Map Selection: 4. Deregistrieren Sie den Point-Handler bei Destruktion (sollte beim Projektschluss sein)
  Component.onDestruction: {
    pointHandler.deregisterHandler("demo2_searchbar");
  }
} 

```

### Randnotiz: console.log

In Demo 1 habe ich iface.logMessage verwendet, um Debug-Anweisungen f√ºr den Benutzer auszugeben.
In Demo 2 habe ich sie durch console.log-Anweisungen ausgetauscht. Diese werden auf das Terminal ausgegeben. Sie k√∂nnen sie sehen, wenn Sie QField von einer DOS- oder Bash-Shell starten, aber sie erscheinen nicht in den Benutzerprotokollen. Dies ist im Allgemeinen sch√∂ner f√ºr den Entwickler.

Das war viel. Schauen wir uns an, wie diese Plot-ID zur MessageBox gelangt. Dieser Teil ist ziemlich einfach.

## üìö **[Handhabung der Plot-ID in der Plugin-Component](DEMO2_PLOTID.md)**
## üìö **[<< Demo2 Einf√ºhrung](DEMO2_INTRO.md)**

````